Let's make a game!
	name:Politcian Clicker
	by:Yiwen
	desc:This is a simple idle game to showcase some of the basic functionalities of Idle Game Maker.<//>Make votes and shoot for the stars!
	created:25/7/2017
	updated:24/10/2017
	version:1

Settings
	background:stuff/corporation.jpg
	building cost increase:115%
	building cost refund:50%
	spritesheet:icons, 48 by 48, https://raw.githubusercontent.com/Tiotao/idlegame/master/imgs/votes_icon.png
	stylesheet:stuff/bigBlue.css

Layout
	use default
		
Buttons
	*voteButton
		name:Make a vote
		desc:Click this little vote to get more votes!
		on click:anim icon wobble
		on click:yield 1 vote
		on click:if (have celeb and chance(1%)) yield 1 opinionVote
		icon:https://raw.githubusercontent.com/Tiotao/idlegame/master/imgs/votes.png
		no text
		class:bigButton hasFlares
		icon class:shadowed
		tooltip origin:bottom
		tooltip class:red
		
Resources
	*vote|votes
		name:Vote|Votes
		desc:These are your votes. You can use them to purchase things. Your goal is to have as many votes as possible!
		icon:icons[0,0]
		class:noBackground
		show earned
		
	*opinionVote|opinionVotes
		name:Opinion Leader Vote|Opinion Leader Votes
		desc:Opinion Leader's votes are terribly rare, terribly precious and terribly useful!
		icon:icons[0,1]
		class:noBackground
		hidden when 0
		
Shinies
	*Voter
		on click:log Woop
		movement:onRight moveLeft fade bounce:0.05
		frequency:60
		frequency variation:30
		icon:https://raw.githubusercontent.com/Tiotao/idlegame/master/imgs/lucky.png
		class:bigButton
		on click:
			$boost=1
			if (have sexual) $boost=4
			if (chance(25%))
				//get at least 7, or between 1 and 3 minutes of our bunny production
				$amount=max(7,random(votes:ps*60*1,votes:ps*60*3))*$boost
				toast An opinion leader's speech get you<//><b>[$amount] votes</b>.
				yield $amount votes
			else
				$amount=1*$boost
				toast An opinion leader decided to vote for you!
				yield $amount opinionVote
			end
		end

Buildings
	*TEMPLATE
		on click:anim glow
		
	*printer|printers
		name:Printer|Printers
		desc:A tiny little printer to print flyers<//><b>Effect:</b><.>Produces 1 vote every 10 seconds.
		icon:icons[3,0]
		cost:15 votes
		on tick:yield 0.1 vote
		unlocked
	
	*assistant|assistants
		name:Campaign assistant|Campaign assistants
		desc:Hire an assistant to print you more posters.<//><b>Effect:</b><.>Produces 1 vote every 2 seconds.
		icon:icons[3,1]
		cost:100 votes
		on tick:yield 0.5 votes
		req:100 votes:earned
	
	*office|offices
		name:Campaign office|Campaign offices
		desc:Rent an office space to start your campaign.<//><b>Effect:</b><.>Produces 5 votes per second.
		icon:icons[3,2]
		cost:600 votes
		on tick:yield 5 votes
		req:600 votes:earned
	
	*newspaper|newspapers
		name:Press|Presses
		desc:A media press that tells good things about you.<//><b>Effect:</b><.>Produces 12 votes per second.
		icon:icons[3,3]
		cost:4000 votes
		on tick:yield 12 votes
		req:4000 votes:earned
	
	*corporation|corporations
		name:Corporation|Corporations
		desc:A profiting corporation that help you raise your campaign fund.<//><b>Effect:</b><.>Produces 90 votes per second.
		icon:icons[3,4]
		cost:20000 votes
		on tick:yield 90 votes
		req:20000 votes:earned
	
	*cabinet|cabinets
		name:Cabinet member|Cabinet members
		desc:Lobby a cabinet member so that your bill gets passed easily<//><b>Effect:</b><.>Produces 300 votes per second.
		icon:icons[3,5]
		cost:200000 votes, 1 opinionVote
		on tick:yield 300 votes
		req:200000 votes:earned and elected
		
	*wall|walls
		name:Great Fire Wall|Great Fire Walls
		desc:People should only see things that you want them to see over the Internet.<//><b>Effect:</b><.>Produces 1000 votes per second.
		icon:icons[3,6]
		cost:3000000 votes, 4 opinionVotes
		on tick:yield 1000 votes
		req:3000000 votes:earned and elected
		
	*miniture|minitures
		name:Miniture|Minitures
		desc:War is Peace. Freedom is Slavery. Ignorance is strength.<//><b>Effect:</b><.>Produces 4000 votes per second.
		icon:icons[3,7]
		cost:70000000 votes, 16 opinionVotes
		on tick:yield 4000 votes
		on tick:if (have long) yield 0.01 opinionVote
		req:70000000 votes:earned and elected
		
	*camp
		name:Dissident's Camp
		text:Freedom camp ([this]%)
		desc:A place where dissidents spent the rest of their life.<//><b>The camp is [this]% complete.</b>
		icon:icons[3,8]
		cost:300000000 votes, 100 opinionVotes
		req:70000000 votes:earned and elected
		limit:100
		cost increase:105%
		
Upgrades
	*TEMPLATE
		on click:anim glow
	 
	//food upgrades
	//inspiration : http://vote.org/suggested-vegetables-and-fruits-for-a-vote-diet/
	
	*educated
		name:Ivy League Degree
		desc:Get another Ivy League Degree to show people that you are smart.<//><b>Effect:</b><.>+1 vote/click
		icon:icons[1,1]
		cost:100 votes
		passive:increase vote yield of voteButton by 1
		req:10 votes:earned
		
	*hair
		name:Stylish Hair
		desc:You need a better hairstyle to attract young voters.<//><b>Effect:</b><.>+1 vote/click
		icon:icons[1,2]
		cost:200 votes
		passive:increase vote yield of voteButton by 1
		req:50 votes:earned
		
	*family
		name:Family Man
		desc:People love to see you hangout with your kids.<//><b>Effect:</b><.>+1 vote/click
		icon:icons[1,3]
		cost:400 votes
		passive:increase vote yield of voteButton by 1
		req:200 votes:earned
		
	*talent
		name:Artistic Talent
		desc:People love to see you playing guitar.<//><b>Effect:</b><.>votes/click x2<.>vote production +5%
		icon:icons[1,4]
		cost:3000 votes
		passive:multiply vote yield of voteButton by 2
		passive:multiply yield of votes by 1.05
		req:1000 votes:earned
		
	*fashion
		name:Fashion Clothing
		desc:Understand fashion increases your exposure in media.<//><b>Effect:</b><.>votes/click x1.5<.>vote production +5%
		icon:icons[1,5]
		cost:10000 votes
		passive:multiply vote yield of voteButton by 1.5
		passive:multiply yield of votes by 1.05
		req:1000 votes:earned
		
	*minority
		name:Minority
		desc:Declare yourself to be minority attracts votes from minority groups.<//><b>Effect:</b><.>votes/click x1.5<.>vote production +5%
		icon:icons[1,6]
		cost:50000 votes
		passive:multiply vote yield of voteButton by 1.5
		passive:multiply yield of votes by 1.05
		req:10000 votes:earned
		
	*plastic
		name:Plastic Surgery
		desc:People love to see pretty faces.<//><b>Effect:</b><.>votes/click x1.5<.>vote production +5%
		icon:icons[1,7]
		cost:100000 votes
		passive:multiply vote yield of voteButton by 1.5
		passive:multiply yield of votes by 1.05
		req:50000 votes:earned
		
	*toastmaster
		name:Toastmaster
		desc:Make an amazing speech.<//><b>Effect:</b><.>votes/click x1.5<.>vote production +5%
		icon:icons[1,8]
		cost:500000 votes
		passive:multiply vote yield of voteButton by 1.5
		passive:multiply yield of votes by 1.05
		req:100000 votes:earned
		
	*biography
		name:Biography
		desc:Write another biography so that you can fake your story or success<//><b>Effect:</b><.>votes/click x1.5<.>vote production +5%
		icon:icons[1,9]
		cost:1000000 votes
		passive:multiply vote yield of voteButton by 1.5
		passive:multiply yield of votes by 1.05
		req:500000 votes:earned
		
	*statue
		name:Statue
		desc:Build a statue for worshiping.<//><b>Effect:</b><.>votes/click x2<.>vote production +10%
		icon:icons[1,0]
		cost:100000000 votes
		passive:multiply vote yield of voteButton by 2
		passive:multiply yield of votes by 1.1
		req:1000000 votes:earned
	
	//building upgrades
	
	*buildingUpgrade1
		name:Full operation
		desc:Now your campaign team works extra time <//><b>Effect:</b><.>printer production x2<.>assistant production x2<.>office production x2
		icon:icons[2,0] icons[3,0]
		cost:1000 votes
		passive:multiply yield of printer by 2
		passive:multiply yield of assistant by 2
		passive:multiply yield of office by 2
		req:(printers>=10 or assistants>=10 or offices>=10)
		
	*buildingUpgrade2
		name:High salary
		desc:Pay more and work more!<//><b>Effect:</b><.>printer production x2<.>assistant production x2<.>office production x2
		icon:icons[2,0] icons[3,1]
		cost:100000 votes
		passive:multiply yield of printer by 2
		passive:multiply yield of assistant by 2
		passive:multiply yield of office by 2
		req:(printers>=50 or assistants>=50 or offices>=50)
		
	*buildingUpgrade3
		name:Performance bonus
		desc:Give bonus, people work harder, more votes coming in.<//><b>Effect:</b><.printer production x2<.>assistant production x2<.>office production x2
		icon:icons[2,0] icons[3,2]
		cost:5000000 votes
		passive:multiply yield of printer by 2
		passive:multiply yield of assistant by 2
		passive:multiply yield of office by 2
		req:(printers>=100 or assistants>=100 or offices>=100)
		
	*buildingUpgrade4
		name:Charity
		desc:Show up in charity events for media exposure, donate small amount of money.<//><b>Effect:</b><.>vote newspaper production x2<.>Press production x2
		icon:icons[2,0] icons[3,3]
		cost:50000 votes
		passive:multiply yield of newspaper by 2
		passive:multiply yield of corporation by 2
		req:(newspapers>=10 or corporations>=10)
		
	*buildingUpgrade5
		name:Monopoly
		desc:Yon own the economy you control people's life.<//><b>Effect:</b><.>newspaper production x2<.>corporation production x2
		icon:icons[2,0] icons[3,4]
		cost:5000000 votes
		passive:multiply yield of newspaper by 2
		passive:multiply yield of corporation by 2
		req:(newspapers>=50 or corporations>=50)
		
	*buildingUpgrade6
		name:Censorship
		desc: Nobody is allowed to speak bad about you.<//><b>Effect:</b><.>cabinet production x2<.>wall production x2
		icon:icons[2,0] icons[3,5]
		cost:1000000 votes
		passive:multiply yield of cabinet by 2
		passive:multiply yield of wall by 2
		req:(cabinets>=10 or walls>=10)
		
	*buildingUpgrade7
		name:Whitelisting
		desc:Only listed website is accessible. How nice!<//><b>Effect:</b><.> cabinet production x2<.> wall production x2
		icon:icons[2,0] icons[3,6]
		cost:500000000 votes
		passive:multiply yield of cabinet by 2
		passive:multiply yield of wall by 2
		req:(cabinets>=50 or walls>=50)
		
	*buildingUpgrade8
		name:Newspeak
		desc:You invented a language that only speakes good about you.<//><b>Effect:</b><.>miniture production x2
		icon:icons[2,0] icons[3,7]
		cost:1000000000 votes
		passive:multiply yield of miniture by 2
		req:10 minitures
		
	//opinion leader vote upgrades
	
	*celeb
		name:Celebrity Friend
		desc:Make friend with a celebrity.<//><b>Effect:</b><.>1% chance of gaining 1 opinion leader vote per vote click
		icon:icons[2,5]
		cost:1 opinionVote
		req:1 opinionVote:earned
		
	*correct
		name:Political Correctness
		desc:Always say things that are political correct<//><b>Effect:</b><.>opinion leader appear 30% more often
		icon:icons[2,6]
		passive:multiply frequency of vote by 0.7
		cost:5 opinionVotes
		req:1 opinionVote:earned
	
	*sexual
		name:Scandal
		desc:Create some scandal that affects your opponents.<//><b>Effect:</b><.>unlocks new buildings
		icon:icons[2,7]
		cost:10 opinionVotes
		req:5 opinionVotes:earned
		
	*elected
		name:President-Elect
		desc:Congradulations! You are the president now.<//><b>Effect:</b><.>opinion leader effects are 4 times more powerful
		icon:icons[2,8]
		cost:100 opinionVotes
		req:50 opinionVotes:earned
		
	*long
		name:Long live
		desc:You invented a way to live forever so that you can rule this country forever<//><b>Effect:</b><.>minitures now produce 1 opinion leader vote every 100 seconds
		icon:icons[2,9]
		cost:100 opinionVotes
		req:50 opinionVotes:earned
		
Achievements
	*TEMPLATE
		on click:anim glow
		